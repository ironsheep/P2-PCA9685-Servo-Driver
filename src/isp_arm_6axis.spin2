'' =================================================================================================
''
''   File....... isp_arm_6axis.spin2
''   Purpose.... Methods for moving our 6-axis arm
''   Authors.... Stephen M Moraco
''               -- Copyright (c) 2021 Iron Sheep Productions, LLC
''               -- see below for terms of use
''   E-mail..... stephen@ironsheep.biz
''   Started.... Jun 2021
''   Updated.... 06 Jun 2021
''
'' =================================================================================================

CON { arm specifics }

    MAX_SERVOS = 8

' which servos are on which PWM channels?
#0, CHAN_SERVO_BASE, CHAN_SERVO_SHOULDER, CHAN_SERVO_ELBOW
#4, CHAN_SERVO_WRIST_FLEX, CHAN_SERVO_WRIST_ROTATE, CHAN_SERVO_GRIP

    CHAN_SERVO_TEST_15 = 15
    CHAN_SERVO_TEST_12 = 12

' servo object indexes
#0, SERVO_BASE, SERVO_SHOULDER, SERVO_ELBOW
#3, SERVO_WRIST_FLEX, SERVO_WRIST_ROTATE, SERVO_GRIP
#6, SERVO_TEST_12, SERVO_TEST_15

    HOME_SERVO_BASE = 90            ' 1500 stopped-moving
    HOME_SERVO_SHOULDER = 109       ' 1645 uSec vertical !!
    HOME_SERVO_ELBOW = 125          ' 1774 uSec vertical !!
    HOME_SERVO_WRIST_FLEX = 95      ' 1535 uSec vertical !!
    HOME_SERVO_WRIST_ROTATE = 94    ' 1530 uSec parallel to arm
    HOME_SERVO_GRIP = 79            ' 1416 uSec mid-way

    HOME_SERVO_TEST_12 = 90
    HOME_SERVO_TEST_15 = 180

    DEFAULT_SERVO_MAX = 2200
    DEFAULT_SERVO_CENTER = 1500
    DEFAULT_SERVO_MIN = 800

    SLEW_WAIT_IN_US = 30000 ' mSec = 30, Sec = 0.03

    DEFAULT_GRIP_SERVO_MIN = 1380   ' open
    DEFAULT_GRIP_SERVO_MAX = 1950   ' closed

#0, MO_NONE, MO_ELBO, MO_VERTICAL, MO_BOT_DOWN, MO_TOP_DOWN, MO_RT_DOWN, MO_FRONT_DOWN, MO_LT_DOWN, MO_BACK_DOWN, MO_ARM_CANTELEVER_F, MO_ARM_CANTELEVER_B

#0, FLT_NONE, FLT_ROLL, FLT_PITCH, FLT_YAW

OBJ { our objects }

    pca9685       : "isp_i2c_pca9685"                         ' backend servo controller (ensure task has access)
    servos[MAX_SERVOS]  : "isp_i2c_pca9685_servo"                   ' our six servos
    term                : "isp_serial_singleton"                 ' terminal output

VAR { TASK VARIABLES & Pins }

    LONG    pin_scl
    LONG    pin_sda
    LONG    pin_oe

    LONG    cog
    LONG    taskStack[32]

pub null()

'' This is not a top-level object

pub start(pinSCL, pinSDA, pinOE) : retCog
    '' Start Servo Slew Controller cog after initializing Arm Servos

    '' Remember the output pins
    pin_scl := pinSCL
    pin_sda := pinSDA
    pin_oe := pinOE

    ' return cog+1 (0 = no cog started)
    cog := retCog := cogspin(NEWCOG, servoSlewTask(), @taskStack) + 1                 ' start s.bus rx/decode cog
    if cog
        '' configure controller then home all arm servos
        'term.fstr0(string("** start() ENTRY\r\n"))

        waitms(3000)   ' hold for 3 secs

        term.fstr1(string("** start() SUCCESS Cg%d\r\n"), retCog - 1)
    else
        term.fstr0(string("** start() FAILURE\r\n"))

PUB init(pinSCL, pinSDA, pinOE)
    '' Initialize the pca output pins (quiet the device so we can trigger LA)

    '' Remember the output pins
    pin_scl := pinSCL
    pin_sda := pinSDA
    pin_oe := pinOE

    pca9685.init(pinSCL, pinSDA, pinOE)

PUB stop()
    '' stop any running back end cogs
    if cog                                                      ' if running
        cogstop(cog-1)                                              '  stop the cog
        term.fstr1(string("** stop() Cg%d\r\n"), cog - 1)
        cog := 0                                                    '  mark stopped

    pca9685.enableOutput(FALSE)


DAT { loop table values }
'                       1/2 1/4  1/8  1/16
delaysUSec      long    707,1414,2829,5657
delaysMaxEntr   long    3   ' ignore 16th for now...

stepsCount      long    60,70,80
stepsMaxEntr    long    3

PUB waggle() | delayEntryIndex, delayRateUSec, stepsIndex, intervalInUsecSteps, servoIndex, tmpServoIndex, targetUSec, pServoName, centerUSec
    '' step thru servos to determine best mopve rates without tipping
    repeat servoIndex from CHAN_SERVO_SHOULDER to CHAN_SERVO_GRIP
        pServoName := servos[servoIndex].name()
        term.fstr1(string("\r\n\r\n--> SERVO [%s]\r\n"), pServoName)
        if servoIndex > CHAN_SERVO_SHOULDER
            repeat tmpServoIndex from CHAN_SERVO_SHOULDER to servoIndex -1
                centerUSec := servos[tmpServoIndex].centerMicroSec()
                servos[tmpServoIndex].slewToPosition(servos.M_USECS, centerUSec)
        repeat stepsIndex from 0 to stepsMaxEntr - 1
            intervalInUsecSteps := long [@stepsCount][stepsIndex]
            'term.fstr1(string("  > %d Steps - full range\r\n"), intervalInUsecSteps)
            servos[servoIndex].setSlewSteps(intervalInUsecSteps)
            repeat delayEntryIndex from 0 to delaysMaxEntr - 1
                delayRateUSec := long [@delaysUSec][delayEntryIndex]
                targetUSec := 800
                term.fstr3(string("  >> target %d, %d Steps, %d uSec Delay\r\n"), targetUSec, intervalInUsecSteps, delayRateUSec)
                servos[servoIndex].slewToPosition(servos.M_USECS, targetUSec)
                continueSlewingUntilDone(delayRateUSec)
                waitms(3000)
                targetUSec := 2200
                term.fstr1(string("  >> target %d\r\n"), targetUSec)
                servos[servoIndex].slewToPosition(servos.M_USECS, targetUSec)
                continueSlewingUntilDone(delayRateUSec)
                waitms(3000)
                term.fstr0(string("\r\n"))

PUB demoPositions(loopCt) | eArmPosition, bDidMove
    '' demo all defined postions in loop, {loopCt} times
    term.fstr1(string("** demoPositions(%d)\r\n"), loopCt)
    if loopCt < 1
        loopCt := 1

    repeat loopCt
        repeat eArmPosition from MO_ELBO to MO_ARM_CANTELEVER_B
            bDidMove := positionArm(eArmPosition)
            if bDidMove
                waitms(3000)   ' hold for 3 secs

    homeArm()


PRI positionArm(eArmPosition) : bDidMove
    '' tell arm to move to specific position
    bDidMove := FALSE
    '#0, MO_NONE, MO_ELBO, MO_VERTICAL, MO_BOT_DOWN, MO_TOP_DOWN, MO_RT_DOWN, MO_FRONT_DOWN, MO_LT_DOWN, MO_BACK_DOWN, MO_ARM_CANTELEVER_F, MO_ARM_CANTELEVER_B
    case eArmPosition
        MO_ELBO:
            'armFlatBackYellowUp()
            'bDidMove := TRUE
        MO_VERTICAL:
            'homeArm()
            'bDidMove := TRUE
        MO_BOT_DOWN:                    '  YELLLOW UP / GREEN DOWN - OFF a bit
            armFlatBackYellowUp()
            bDidMove := TRUE
        MO_TOP_DOWN:                    '  GREEN UP / YELLLOW DOWN
            armFlatForwardGreenUp()
            bDidMove := TRUE
        MO_RT_DOWN:                     '  ORANGE UP / RED DOWN  - tilted forward, tilted right
            armFlatForwardOrangeUp()
            bDidMove := TRUE
        MO_FRONT_DOWN:                  '  CYAN UP / PURPLE DOWN
            armVertCyanUp()
            bDidMove := TRUE
        MO_LT_DOWN:                     '  RED UP / RED ORANGE  - tiled right
            armFlatBackRedUp()              ' GOOD
            bDidMove := TRUE
        MO_BACK_DOWN:                   '  PURPLE UP / CYAN DOWN
            armFlatBackVioletUp()
            bDidMove := TRUE
        MO_ARM_CANTELEVER_F:
            'armFlatForward()
            'bDidMove := TRUE
        MO_ARM_CANTELEVER_B:
            'armFlatBack()
            'armFlatBackYellowUp()
            'bDidMove := TRUE
        other:
            bDidMove := FALSE


{
    repeat
        servos[SERVO_TEST_15].write(servos.M_USECS, 900)
        waitms(1000)
        servos[SERVO_TEST_15].write(servos.M_USECS, 1500)
        waitms(1000)
        servos[SERVO_TEST_15].write(servos.M_USECS, 2200)
        waitms(1000)


    repeat
        repeat angle from 0 to 180 step 20
            waitms(500)
            servos[SERVO_TEST_15].write(servos.M_ANGLE, angle)
            waitms(200)

        waitms(1000)
        servos[SERVO_TEST_15].write(servos.M_ANGLE, 0)

    term.fstr0(string("** doServoSteps() EXIT\r\n\r\n"))
'}

    ' -----------------------------------------------------------------
    ' calibrate clock... pulse width
    {
    'servos[SERVO_TEST_12].start(CHAN_SERVO_TEST_12, HOME_SERVO_TEST_12)
    servos[SERVO_TEST_15].start(CHAN_SERVO_TEST_15, HOME_SERVO_TEST_15)
    waitms(8000)
    servos[SERVO_TEST_15].write(servos.M_ANGLE, 0)
    waitms(8000)

    servos[SERVO_TEST_15].write(servos.M_USECS, 800)
    waitms(8000)
    servos[SERVO_TEST_15].write(servos.M_USECS, 1500)
    waitms(8000)
    servos[SERVO_TEST_15].write(servos.M_USECS, 2200)

    repeat
    '}
    ' -----------------------------------------------------------------


PUB showservoPositions() | servoIndex, posnUSec, posnDegr
    '' Dump all servo values to term
    repeat servoIndex from SERVO_BASE to SERVO_GRIP
        if servos[servoIndex].isActive()
            servos[servoIndex].showPosition()

PUB gripClosed()
    '' Close the jaws
    'term.fstr0(string("** gripClosed()\r\n"))
    servos[SERVO_GRIP].slewToPosition(servos.M_USECS, DEFAULT_GRIP_SERVO_MAX)

PUB gripMidOpen()
    '' Open the jaws half-way
    'term.fstr0(string("** gripOpen()\r\n"))
    servos[SERVO_GRIP].slewToPosition(servos.M_USECS, servos[SERVO_GRIP].centerMicroSec())

PUB gripOpen()
    '' Open the jaws
    'term.fstr0(string("** gripOpen()\r\n"))
    servos[SERVO_GRIP].slewToPosition(servos.M_USECS, DEFAULT_GRIP_SERVO_MIN)

PUB positionServo(eServo, valueInUSec, minRange, maxRange) | servoIndex, newPosnUSec
    '' Move servo to specified position (limited by its own range)
    servoIndex := SERVO_BASE #> eServo <# SERVO_GRIP   ' must be one of our 6 servos
    newPosnUSec:= servos[servoIndex].mapValueToServoPosnInUSec(valueInUSec, minRange, maxRange)
    servos[servoIndex].slewToPosition(servos.M_USECS, newPosnUSec)

PUB doServoSteps() | angle
    '' TEST METHOD stepping a single servo
    ' for( int angle =0; angle<181; angle +=20) {
    '    delay(500);
    '    pwm.setPWM(0, 0, angleToPulse(angle)
    ' };
    term.fstr0(string("\r\n** doServoSteps() ENTRY\r\n"))
    repeat
        repeat angle from 0 to 180 step 20
            waitms(500)
            servos[SERVO_TEST_15].writePosition(servos.M_ANGLE, angle)
        waitms(1000)

    term.fstr0(string("** doServoSteps() EXIT\r\n\r\n"))

PUB quietController()
    '' Setup pins so signal is quiet so we can trigger our LA
    term.fstr0(string("** quietController()\r\n"))

    pca9685.enableDebug(TRUE)
    pca9685.init(pin_scl, pin_sda, pin_oe)  ' set pins to output low... so we can set LA trigger

CON  { ---- PRIVATE Slew Control TASK ---- }


PRI servoSlewTask() : ok | bDidMove, servoIndex, tmpID
    ' this is our servo slewing code...
    '  we are slewing to keep our arm momentum/inertia low
    tmpID := cogid()
    term.fstr2(string("** servoSlewTask() running in Cg%d (0x%.4x) **\r\n"), tmpID, tmpID)
    taskInit()
    ok := taskInitController()
    if ok
        ' set pwm freq...
        pca9685.configureController()

            ' define servo ranges and such
        taskInitArmServos()
        ' place arm vertical to start with (this provides us with initial servo position as well)
        taskForceArmHome()

        repeat
            bDidMove := FALSE
            repeat servoIndex from SERVO_BASE to SERVO_GRIP
                if servos[servoIndex].isActive()
                    if not servos[servoIndex].isAtPostion()
                        if servos[servoIndex].delayExpired()
                            servos[servoIndex].continueSlew()
                            bDidMove := TRUE
            if bDidMove
                waitus(SLEW_WAIT_IN_US)    ' 25 sorta ok, 10 bad, 20 sorta ok too (too much rocking)
    else
        term.fstr0(string("** servoSlewTask() startup failed (no pwm dvc)\r\n"))
        abort -1

PRI taskInit()
    term.fstr1(string("** taskInit() Cg%d\r\n"), cogid())
    pca9685.enableDebug(TRUE)
    pca9685.init(pin_scl, pin_sda, pin_oe)  ' set pins to output low... so we can set LA trigger

PRI taskInitController() : ok | deviceFound
    ' find and configure our mpu9dof device

    term.fstr0(string("** taskInitController() Setup...\r\n"))
    pca9685.enableDebug(TRUE)
    deviceFound := pca9685.start(pin_scl, pin_sda, pin_oe)                                           ' i2c @ 100kHz, no pull-ups
    term.fstr1(string("Start Found Addr:(%d)\r\n"), deviceFound - 1)

    term.fstr0(string("ID device address...\r\n"))
    deviceFound := pca9685.deviceID()
    ok := TRUE
    if(deviceFound <> pca9685.DVC_NOT_FOUND)
        term.fstr1(string("dvcID Found Addr:(%d)\r\n"), deviceFound - 1)
    else
        term.fstr0(string("ERROR: Found no devices\r\n"))
        ok := FALSE
        abort -1

    pca9685.enableDebug(FALSE)
    pca9685.enableOutput(TRUE)
    'term.fstr0(string("\r\nAccess PCA9685 16-chan PWM ...\r\n\r\n"))

PRI taskInitArmServos()
    ' configure all the servo objects with channel and limits specific to each
    pca9685.enableDebug(FALSE)
    term.fstr0(string("\r\n** taskInitArmServos() ENTRY\r\n"))
    servos[SERVO_BASE].configureMinCtrMaxType(DEFAULT_SERVO_MIN, DEFAULT_SERVO_CENTER, DEFAULT_SERVO_MAX, servos.ST_360_DEGREE)
    servos[SERVO_BASE].init(CHAN_SERVO_BASE, servos.M_NOT_SET, 0, string("Base"))
    servos[SERVO_BASE].enableDebug(TRUE)

    servos[SERVO_SHOULDER].configureMinCtrMaxType(DEFAULT_SERVO_MIN, 1640, DEFAULT_SERVO_MAX, servos.ST_STANDARD)
    servos[SERVO_SHOULDER].init(CHAN_SERVO_SHOULDER, servos.M_NOT_SET, 0, string("Shldr"))
    servos[SERVO_SHOULDER].enableDebug(TRUE)
    servos[SERVO_GRIP].setSlewSteps(70)

    servos[SERVO_ELBOW].configureMinCtrMaxType(DEFAULT_SERVO_MIN, 1800, DEFAULT_SERVO_MAX, servos.ST_STANDARD)
    servos[SERVO_ELBOW].init(CHAN_SERVO_ELBOW, servos.M_NOT_SET, 0, string("Elbow"))
    servos[SERVO_ELBOW].enableDebug(TRUE)
    servos[SERVO_GRIP].setSlewSteps(70)

    servos[SERVO_WRIST_FLEX].configureMinCtrMaxType(DEFAULT_SERVO_MIN, 1547, DEFAULT_SERVO_MAX, servos.ST_STANDARD)
    servos[SERVO_WRIST_FLEX].init(CHAN_SERVO_WRIST_FLEX, servos.M_NOT_SET, 0, string("WrFlex"))
    servos[SERVO_WRIST_FLEX].enableDebug(TRUE)
    servos[SERVO_GRIP].setSlewSteps(50)

    servos[SERVO_WRIST_ROTATE].configureMinCtrMaxType(DEFAULT_SERVO_MIN, 1540, DEFAULT_SERVO_MAX, servos.ST_STANDARD)
    servos[SERVO_WRIST_ROTATE].init(CHAN_SERVO_WRIST_ROTATE, servos.M_NOT_SET, 0, string("WrRota"))
    servos[SERVO_WRIST_ROTATE].enableDebug(TRUE)
    servos[SERVO_WRIST_ROTATE].setSlewSteps(25)

    servos[SERVO_GRIP].configureMinCtrMaxType(DEFAULT_GRIP_SERVO_MIN, servos.CALCULATE_CENTER, DEFAULT_GRIP_SERVO_MAX, servos.ST_STANDARD)
    servos[SERVO_GRIP].init(CHAN_SERVO_GRIP, servos.M_NOT_SET, 0, string("Grip"))
    servos[SERVO_GRIP].enableDebug(TRUE)
    servos[SERVO_GRIP].setSlewSteps(25)
    term.fstr0(string("** taskInitArmServos() EXIT\r\n\r\n"))
    pca9685.enableDebug(FALSE)

PRI taskForceArmHome() | centerUSec, servoIndex
    '' place the arm completely upright at start of run
    pca9685.enableDebug(FALSE)
    term.fstr0(string("\r\n** taskForceArmHome() ENTRY\r\n"))
    servos[SERVO_BASE].writePosition(servos.M_ANGLE, HOME_SERVO_BASE)
    repeat servoIndex from SERVO_SHOULDER to SERVO_WRIST_ROTATE
        centerUSec := servos[servoIndex].centerMicroSec()
        servos[servoIndex].writePosition(servos.M_USECS, centerUSec)
    servos[SERVO_GRIP].writePosition(servos.M_USECS, DEFAULT_GRIP_SERVO_MAX)
    ' wait for servos to arrive at target positions
    waitms(1000)    ' sleep 1 second
    term.fstr0(string("** taskForceArmHome() EXIT\r\n\r\n"))

PUB homeArm() | centerUSec, servoIndex
    '' place the arm completely upright at start of run
    term.fstr0(string("** homeArm() ENTRY\r\n"))
    servos[SERVO_BASE].slewToPosition(servos.M_USECS, 1500)         '  90 degr
    repeat servoIndex from SERVO_SHOULDER to SERVO_WRIST_ROTATE
        centerUSec := servos[servoIndex].centerMicroSec()
        servos[servoIndex].slewToPosition(servos.M_USECS, centerUSec)
    servos[SERVO_GRIP].slewToPosition(servos.M_USECS, DEFAULT_GRIP_SERVO_MAX)         ' 180 degr - closed
    ' wait for servos to arrive at target positions
    continueSlewingUntilDone(SLEW_WAIT_IN_US)
    term.fstr0(string("** homeArm() EXIT\r\n\r\n"))

CON  { ---- PRIVATE Utility Methods ---- }

PRI armFlatBackYellowUp()
    ' place the arm bent at 90 degr overhanging base
    ' YELLOW UP
    term.fstr0(string("** armFlatBackYellowUp()\r\n"))
    servos[SERVO_SHOULDER].slewToPosition(servos.M_USECS, 1430)     ' 79 degr back
    servos[SERVO_ELBOW].slewToPosition(servos.M_USECS, 800)         ' 0 degr back
    servos[SERVO_WRIST_FLEX].slewToPosition(servos.M_USECS, 1538)   ' 95 degr
    servos[SERVO_WRIST_ROTATE].slewToPosition(servos.M_USECS, 1560) ' 99 degr
    ' wait for servos to arrive at target positions
    continueSlewingUntilDone(SLEW_WAIT_IN_US)

PRI armFlatForwardOrangeUp()
    ' arm flat 2nd means
    ' ORANGE UP
    term.fstr0(string("** armFlatForwardOrangeUp()\r\n"))
    servos[SERVO_SHOULDER].slewToPosition(servos.M_USECS, 1176) ' fwd
    servos[SERVO_ELBOW].slewToPosition(servos.M_USECS, 2136)     ' was 8 fwd
    servos[SERVO_WRIST_ROTATE].slewToPosition(servos.M_USECS, 840)   ' Orig 900, 850, 840 fwd
    ' wait for servos to arrive at target positions
    continueSlewingUntilDone(SLEW_WAIT_IN_US)

PRI armFlatBackVioletUp()
    ' place the arm bent at 90 degr overhanging base
    ' VIOLET UP
    term.fstr0(string("** armFlatBackVioletUp()\r\n"))
    servos[SERVO_SHOULDER].slewToPosition(servos.M_USECS, 2200)         ' 180 degr back
    servos[SERVO_ELBOW].slewToPosition(servos.M_USECS, 1503)             ' 90 degr back
    servos[SERVO_WRIST_FLEX].slewToPosition(servos.M_USECS, 800)        ' 0 degr
    servos[SERVO_WRIST_ROTATE].slewToPosition(servos.M_USECS, 800)     ' 0 degr
    ' wait for servos to arrive at target positions
    continueSlewingUntilDone(SLEW_WAIT_IN_US)

PRI armVertCyanUp()
    ' CYAN UP
    homeArm()

PRI armFlatBack()
    ' arm flat 1st means
    term.fstr0(string("** armFlatBack()\r\n"))
    servos[SERVO_SHOULDER].slewToPosition(servos.M_USECS, 1430)     ' 90 degr back
    servos[SERVO_ELBOW].slewToPosition(servos.M_USECS, 800)         ' 12 degr back
    ' wait for servos to arrive at target positions
    continueSlewingUntilDone(SLEW_WAIT_IN_US)

PRI armFlatBackRedUp()
    ' arm flat 1st means
    ' RED UP
    term.fstr0(string("** armFlatBackRedUp()\r\n"))
    servos[SERVO_SHOULDER].slewToPosition(servos.M_USECS, 1430) ' back
    servos[SERVO_ELBOW].slewToPosition(servos.M_USECS, 800)    ' back
    servos[SERVO_WRIST_ROTATE].slewToPosition(servos.M_USECS, 800)   ' 2300 -> 800 when back
    ' wait for servos to arrive at target positions
    continueSlewingUntilDone(SLEW_WAIT_IN_US)

PRI armFlatForwardGreenUp()
    ' arm flat 1st means
    ' GREEN UP
    term.fstr0(string("** armFlatForwardGreenUp()\r\n"))
    servos[SERVO_SHOULDER].slewToPosition(servos.M_USECS, 1176) ' fwd
    servos[SERVO_ELBOW].slewToPosition(servos.M_USECS, 2136)    ' fwd
    servos[SERVO_WRIST_ROTATE].slewToPosition(servos.M_USECS, 1550)  ' orig 1500
    ' wait for servos to arrive at target positions
    continueSlewingUntilDone(SLEW_WAIT_IN_US)


PRI armFlatForward()
    ' arm flat 2nd means
    term.fstr0(string("** armFlatForward()\r\n"))
    ' 1700 low
    servos[SERVO_SHOULDER].slewToPosition(servos.M_USECS, 1176)     ' fwd
    servos[SERVO_ELBOW].slewToPosition(servos.M_USECS, 2136)        ' fwd
    ' wait for servos to arrive at target positions
    continueSlewingUntilDone(SLEW_WAIT_IN_US)

PRI continueSlewingUntilDone(slewDelayInUSec) | bSlewComplete
    '' loop until all servos are at their target
    repeat
            waitus(slewDelayInUSec)    ' 25 sorta ok, 10 bad, 20 sorta ok too (too much rocking)
            bSlewComplete := allServosAtTarget()
    while not bSlewComplete
    'term.fstr0(string("** continueSlewingUntilDone() DONE\r\n\r\n"))


PRI allServosAtTarget() : bAtTArgetStatus | servoIndex, bDoneStatus
    bAtTArgetStatus := TRUE
    repeat servoIndex from SERVO_BASE to SERVO_GRIP
        bDoneStatus := servos[servoIndex].isAtPostion()
        if not bDoneStatus
            bAtTArgetStatus := FALSE
            quit    ' outta here, we know our answer
    'if bAtTArgetStatus
    '    term.fstr0(string("** allServosAtTarget() DONE\r\n"))
    'else
    '    term.fstr0(string("** allServosAtTarget() waiting...\r\n"))



CON { license }

{{
 -------------------------------------------------------------------------------------------------
  MIT License

  Copyright (c) 2021 Iron Sheep Productions, LLC

  Permission is hereby granted, free of charge, to any person obtaining a copy
  of this software and associated documentation files (the "Software"), to deal
  in the Software without restriction, including without limitation the rights
  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
  copies of the Software, and to permit persons to whom the Software is
  furnished to do so, subject to the following conditions:

  The above copyright notice and this permission notice shall be included in all
  copies or substantial portions of the Software.

  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
  SOFTWARE.
 =================================================================================================
}}
